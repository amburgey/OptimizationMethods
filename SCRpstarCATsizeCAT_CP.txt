
model {

  for(l in 1:4){
    p0[l] ~ dunif(0,1)
  }
  alpha0 <- logit(p0)
  sigma ~ dunif(0,100)
  alpha1 <- 1/(2*sigma*sigma)
  
  # Posterior conditional distribution for N-n (and hence N):
  n0 ~ dnegbin(pstar,n)  # number of failures
  N <- n + n0  # successful observations plus failures to observe = total N
  
  #Probability of capture for integration grid points
  #pdot = probability of being detected at least once (given location)

  ## Removed k loop
  for(i in 1:n){
    for(g in 1:Gpts){ # Gpts = number of points on integration grid
      for(j in 1:J){  # J = number of traps
        #Probability of being missed at grid cell g and trap j multiplied by total effort (K) at that trap
        one_minus_detprob[i,g,j] <- 1 - p0[size[i]]*exp(-alpha1*Gdist[g,j]*Gdist[g,j])*K[j] #Gdist given as data
      } #J
      pdot.temp[i,g] <- 1 - prod(one_minus_detprob[i,g,]) #Prob of failure to detect across entire study area and time period
      pdot[i,g] <- max(pdot.temp[i,g], 1.0E-10)  #pdot.temp is very close to zero and will lock model up with out this
    } #G
    pstar[i] <- (sum(pdot[i,1:Gpts])*a)/A   #prob of detecting an individual at least once in S (a=area of each integration grid, given as data)
  } #I
  
  
  ##### NO CHANGING, TO MAKE JAGS/BUGS LIKELIHOOD FUNCTION PROPERLY ##### 
  # Zero trick for initial 1/pstar^n
  loglikterm <- -n * log(pstar)
  lambda <- -loglikterm + 1000
  dummy ~ dpois(lambda) # dummy = 0; entered as data
  ##### NO CHANGING, TO MAKE JAGS/BUGS LIKELIHOOD FUNCTION PROPERLY #####

  # prior prob for each grid cell (setting b[1:Gpts] = rep(1,Gpts) is a uniform prior across all cells)   
  pi[1:Gpts] ~ ddirch(b[1:Gpts])
 
  for(i in 1:n){  ## n = number of observed individuals
    ## For use when defining traps on a grid cell
    s[i] ~ dcat(pi[1:Gpts])
    
    # Model for capture histories of observed individuals:
    for(j in 1:J){  ## J = number of traps
      y[i,j] ~ dbin(p[i,j],K[j])
      d[i,j] <- Gdist[s[i],j]
      p[i,j] <- p0[size[i]]*exp(-alpha1*d[i,j]*d[i,j])
    }#J
  }#n
}
