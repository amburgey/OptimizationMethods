
  model {
    
    #Prior for spatial decay
    sigma ~ dunif(0,100)
    alpha1 <- 1/(2*sigma*sigma)
    #Prior for each grid cell (set b[1:Gpts] = rep(1,Gpts) below so as to be a uniform prior across all cells)
    pi[1:Gpts] ~ ddirch(b[1:Gpts])
    
    for(l in 1:L){      # 4 size categories
      #Prior for intercept
      p0V[l] ~ dunif(0,5)  # VIS
      p0T[l] ~ dunif(0,5)  # TRAP
        
      #Prior for N
      #Posterior conditional distribution for N-n (and hence N):
      n0[l] ~ dnegbin(pstar[l],ngroup[l])  # number of failures to observe by project and size category
      Ngroup[l] <- ngroup[l] + n0[l]       # number of animals observed + failures to observe by size category
    }
    
    N <- sum(Ngroup[1:L])  # successful observations plus failures to observe of each size = total N
    
    for(l in 1:L){  # size category
      for(g in 1:Gpts){  # number of points on integration grid
        for(j in 1:J1){  # number of survey points
          #Probability of an individual of size i being missed at grid cell g and point j multiplied by total effort (K) at that location
          miss_allKV[l,g,j] <- pow((1 - p0V[l]*exp(-alpha1*GdistV[g,j]*GdistV[g,j])),K)  # prob missed by visual searches
        } #J1
        for(j in 1:J2){  # number of trapping points
          #Probability of an individual of size i being missed at grid cell g and trap j multiplied by total effort (K) at that trap
          miss_allKT[l,g,j] <- pow((1 - p0T[l]*exp(-alpha1*GdistT[g,j]*GdistT[g,j])),K)  # prob missed by trapping
        } #J2
        #Probability of detecting each size category across all points in the study area and time period
        pdot.temp[l,g] <- 1 - prod(miss_allKV[l,g,],miss_allKT[l,g,])
        pdot[l,g] <- max(pdot.temp[l,g], 1.0E-10)  # pdot.temp is very close to zero and will lock model up with out this
      } #G
      #Probability of detecting a size category at least once in S (a=area of each integration grid, given as data)
      pstar[l] <- (sum(pdot[l,1:Gpts]*a))/A
      
      #Zero trick for initial 1/pstar^n, described in King et al. 2016
      loglikterm[l] <- -ngroup[l] * log(pstar[l])
      lambda[l] <- -loglikterm[l] + 10000
      dummy[l] ~ dpois(lambda[l]) # dummy = 0; entered as data
    } #L
    
    for(i in 1:n){  # number of observed individuals for each project
      #Define activity centers on a grid
      s[i] ~ dcat(pi[1:Gpts])
    }
    
    for(i in 1:nV){  # number of individuals observed via visual surveys
      #Model for capture histories of observed individuals from visual surveys:
      for(j in 1:J1){  # number of survey points
        yV[i,j] ~ dpois(pV[i,j]*K)
        pV[i,j] <- p0V[size[indV[i]]]*exp(-alpha1*GdistV[s[indV[i]],j]*GdistV[s[indV[i]],j])
      }#JV
    }#I
    
    for(i in 1:nT){  # number of individuals observed via trapping surveys
      #Model for capture histories of observed individuals from traps:
      for(j in 1:J2){  # number of trapping points
        yT[i,j] ~ dpois(pT[i,j]*K)
        pT[i,j] <- p0T[size[indT[i]]]*exp(-alpha1*GdistT[s[indT[i]],j]*GdistT[s[indT[i]],j])
      }#JT
    }#I
    
    #Derived proportion of each size class
    for(l in 1:L){
      piGroup[l] <- Ngroup[l]/N
    }
  }
  